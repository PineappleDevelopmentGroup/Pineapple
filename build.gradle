plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "io.freefair.aggregate-javadoc-jar" version "8.4"
}

// shadowJar takes care of this
jar.enabled = true

dependencies {
    implementation(project(":pineapple-utils"))
    implementation(project(":pineapple-chat"))
    implementation(project(":pineapple-core"))
    implementation(project(":pineapple-nms"))
}

subprojects {
    apply plugin: 'java'

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

        compileOnly("org.jetbrains:annotations-java5:24.0.1")
    }

    test {
        useJUnitPlatform()

        testLogging {
            events("passed", "skipped", "failed")
        }
    }

    aggregateJavadocJar {
        configure {
            exclude "sh/miles/pineapple/nms/impl/**"
        }
    }

    javadoc {
        configure {
            exclude "sh/miles/pineapple/nms/impl/**"
        }
    }
}

shadowJar {
    archiveClassifier.set('')
    archiveVersion.set('')

    relocate('com.jeff_media.morepersistentdatatypes', 'sh.miles.pineapple.pdc.morepdc')
    relocate('org.bstats.bstats-bukkit', 'sh.miles.pineapple.bstats.bstats')
}

build {
    dependsOn shadowJar
}

aggregateJavadoc.options {
    addStringOption('Xdoclint:none', '-quiet')
    links(
            'https://docs.oracle.com/en/java/javase/17/docs/api/',
            'https://hub.spigotmc.org/javadocs/spigot/',
            'https://javadoc.io/doc/org.jetbrains/annotations-java5/24.0.1',
            'https://repo.jeff-media.com/javadoc/public/com/jeff_media/MorePersistentDataTypes/2.4.0/raw/'
    )
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            publication.artifact(aggregateJavadocJar)

            repositories {
                maven {
                    url = 'https://maven.miles.sh/libraries'
                    credentials {
                        username = System.getenv('PINEAPPLE_REPOSILITE_USERNAME')
                        password = System.getenv('PINEAPPLE_REPOSILITE_PASSWORD')
                    }
                }
            }
        }
    }
}
